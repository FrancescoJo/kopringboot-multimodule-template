import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

/**
 * Groupising tests for JUnit5
 *
 * https://docs.gradle.org/current/userguide/java_testing.html#test_grouping
 * https://junit.org/junit5/docs/current/user-guide/#writing-tests-tagging-and-filtering
 */
def TAG_TEST_SMALL = "smallTest"
def TAG_TEST_MEDIUM = "mediumTest"
def TAG_TEST_LARGE = "largeTest"

dependencies {
    // JUnit5
    testImplementation "org.junit.jupiter:junit-jupiter-api:$version_junit5"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$version_junit5"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$version_junit5"

    // Test utilities
    testImplementation "org.hamcrest:hamcrest:$version_hamcrest"
    testImplementation "org.mockito:mockito-core:$version_mockito"
    testImplementation "org.mockito:mockito-inline:$version_mockito"
    testImplementation "org.mockito:mockito-junit-jupiter:$version_mockito"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$version_mockitoKotlin"

    // JavaFaker
    testImplementation("com.github.javafaker:javafaker:$version_javaFaker") {
        exclude group: "org.yaml", module: "snakeyaml"
    }
    testImplementation("org.yaml:snakeyaml:1.30")

    // TestContainers
    testImplementation("org.testcontainers:testcontainers:$version_testcontainers") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
}

check.dependsOn test

configurations {
    testArtifacts.extendsFrom testRuntime
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}

tasks.withType(Test) {
    group "verification"

    testLogging {
        events TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR

        outputs.upToDateWhen { false }
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

test {
    useJUnitPlatform()
}

task smallTest(type: Test) {
    useJUnitPlatform {
        includeTags TAG_TEST_SMALL
        excludeTags "*"
    }
}

task mediumTest(type: Test) {
    useJUnitPlatform {
        includeTags TAG_TEST_MEDIUM
        excludeTags "*"
    }
}

task largeTest(type: Test) {
    useJUnitPlatform {
        includeTags TAG_TEST_LARGE
        excludeTags "*"
    }
}

// https://docs.gradle.org/current/userguide/java_testing.html#test_reporting
configurations {
    testReportData {
        canBeResolved = true
        canBeConsumed = false
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'test-report-data'))
        }
    }
}

tasks.register("testReport", TestReport) {
    destinationDir = layout.buildDirectory.dir("reports/allTests").get().asFile
    // Use test results from testReportData configuration
    testResultDirs.from(configurations.testReportData)
}
